/*
 * gradle build script for schema2doc project.
 *
 * uses gradle 2.6 for compile (or provided gradlew script).
 *
 * @see     http://man-at-home.github.io/schema2doc/
 * @author  man-at-home
 * @since   2015
 * @version 2015
 */


buildscript {

   repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
    
    dependencies {
//      classpath "org.asciidoctor:asciidoctor-gradle-plugin:1.5.2" 							// asciidoctor doc
        classpath "org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.9"
        classpath "org.flywaydb:flyway-gradle-plugin:3.2.1"										// db migration
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.3.1"						// upload
    }
}

plugins {
        id 'org.asciidoctor.convert' version '1.5.2'
        id 'com.github.jruby-gradle.base' version '0.3.0'
}


group   = 'org.manathome'
version = '0.1.5'

// development
apply plugin: "java"
apply plugin: "eclipse"
apply plugin: "org.flywaydb.flyway"
 
// qa
apply plugin: "checkstyle"
apply plugin: "findbugs"     
apply plugin: "jacoco"
apply plugin: "jdepend"
apply plugin: "pmd"
apply plugin: "sonar-runner"

// documentation
apply plugin: "project-report"
apply plugin: "build-dashboard"
apply plugin: "announce"
apply plugin: "org.asciidoctor.gradle.asciidoctor"

// deployment
apply plugin: "maven"
apply plugin: "maven-publish"
apply plugin: "com.jfrog.bintray"


repositories {
        mavenLocal()
        mavenCentral()
        
        maven{url "http://gradle.artifactoryonline.com/gradle/libs/"}
        jcenter()        
}

sourceCompatibility = "1.8"

compileJava {
   targetCompatibility = 1.8
}

dependencies {       
   compile "org.slf4j:slf4j-api:1.7.12"                      // logging api
   compile "ch.qos.logback:logback-classic:1.1.3"            // logging library
   compile "commons-cli:commons-cli:1.3"                     // command line parser 
   
   testCompile "junit:junit:4.12"							 // unit tests
   testCompile "com.h2database:h2:1.4.187"					 // dev database (usable in memory)
   
   compile fileTree(dir: 'lib', include: ['*.jar'])

   gems 'rubygems:asciidoctor-diagram:1.2.1'
}


jdepend {
   toolVersion = "2.9.1"
}

jdependMain {
   reports {
      xml.enabled  = false
      text.enabled = true
   }
}

jdependTest {
   reports {
      xml.enabled  = false  
      text.enabled = true
   }
}

findbugs {
   toolVersion    = "3.0.1"
   ignoreFailures = true
   effort         = "max"
   reportLevel    = "high"
}

checkstyle {
   ignoreFailures = true      
   toolVersion    = "6.7"    // with java8 support
}

pmd {
   ignoreFailures = true
}

flyway {
    url = "jdbc:h2:./schema2doc.test.h2.db"  // makes no sense with the dev in memory db.
    user = 'sa'
}


tasks.withType(FindBugs) {
    reports {
        xml.enabled  = false
        html.enabled = true
    }
}

jacocoTestReport {
    reports {
        xml.enabled      = true    // required for sonar&coveralls
        html.enabled     = true
        csv.enabled      = false
//        html.destination = "${buildDir}/reports/jacoco"
    }
}

// configured for local sonarQube 4.5 installation (defaults)
sonarRunner {
//    toolVersion = '2.4'
    sonarProperties {
        property "sonar.projectName",          "totask2 project time logging"
        property "sonar.projectKey",           "totask2"
        property "sonar.jacoco.reportPath",    "${buildDir}/jacoco/test.exec"
        property "sonar.junit.reportsPath",    "${buildDir}/test-results/"
        property "sonar.surefire.reportsPath", "${buildDir}/test-results/"
    }
}


asciidoctor {
    sourceDir    = new File("${projectDir}/src/docs")
    outputDir    = new File("${buildDir}/docs/asciidoc")
    logDocuments = true
    // backends 'html5'
    backends    'html5', 'pdf'
    
    
    dependsOn jrubyPrepareGems
    requires = ['asciidoctor-diagram']
    gemPath = jrubyPrepareGems.outputDir
} 


javadoc {
    failOnError         = false
    options.windowTitle = "totask2 api documentation ${version}"	
    options.links( "http://download.oracle.com/javase/8/docs/api/" )
    options.addBooleanOption("Xdoclint:none").setValue(true)
}

// temporary shortcut, deploy directly for plugin use
//           target solution: multi project build with dependencies..
uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: uri('../schema2doc-plugin/repo'))
        }
    }
}

task sourceJar(type: Jar, dependsOn: classes) {
     description "generate a jar with all source files"
     classifier = "sources"
     from sourceSets.main.allSource
}
task javadocJar(type: Jar) {
     description "generate a jar with all source files"
     classifier = "javadoc"
     from javadoc.destinationDir
     dependsOn javadoc
}


// add javadoc/source jars as artifacts
artifacts {
     archives sourceJar, javadocJar
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
     		
     		artifact sourceJar 
            artifact javadocJar
        }
    }
    repositories {
        maven {
            // project local dummy repo
            url "$buildDir/repo"
        }
    }
}


// bintrayUpload task
// ------------------
bintrayUpload.dependsOn(assemble)

bintray {
    user = "man-at-home" 
    key  = System.getenv("BINTRAY_KEY")
 
    publications = ['mavenJava']
    dryRun 		 = false			// change to false for actual upload to bintray
    publish 	 = false			// keep publishing step manual for the time beeing
    pkg {
        repo 			= "maven"
        name 			= "schema2doc"
        desc 			= "an asciidoc db schema documentation generator"
        websiteUrl 		= "https://github.com/man-at-home/schema2doc"
        issueTrackerUrl = "https://waffle.io/man-at-home/schema2doc"
        vcsUrl 			= "https://github.com/man-at-home/schema2doc.git"
        licenses 		= ['Apache-2.0']
        publicDownloadNumbers = false
        
        version {
                name = '0.1.5'					// adapt
                desc = 'under development'
        }
    }
}




// CUSTOM TASKS
// ============

task allDocs {
    description "generation of full documentation (asciidoc and javadoc)"
    dependsOn javadoc, asciidoctor, projectReport 
}

task allTests {
    description "all tests fresh"
    dependsOn clean, flywayClean, flywayMigrate, test, jacocoTestReport
}

task coverage {
    description "code coverage"
    dependsOn test, jacocoTestReport
}

task wrapper(type: Wrapper) {
    description "gradle wrapper generation (call if gradle version changes)"
    gradleVersion = '2.6'
}



task publish2public {
    description "upload jars to jcenter (maven repo)"
    dependsOn bintrayUpload
}