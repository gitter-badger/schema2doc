/*
 * gradle build script for schema2doc project.
 *
 * uses gradle 2.5 for compile (or provided gradlew script).
 *
 * @see     http://man-at-home.github.io/schema2doc/
 * @author  man-at-home
 * @since   2015
 * @version 2015
 */

// development
apply plugin: "java"
apply plugin: "eclipse"
 
// qa
apply plugin: "checkstyle"
apply plugin: "findbugs"     
apply plugin: "jacoco"
apply plugin: "jdepend"
apply plugin: "pmd"
apply plugin: "sonar-runner"

// documentation
apply plugin: "project-report"
apply plugin: "build-dashboard"
apply plugin: "announce"
apply plugin: "org.asciidoctor.gradle.asciidoctor"



repositories {
        mavenLocal()
        mavenCentral()
        
        maven{url "http://gradle.artifactoryonline.com/gradle/libs/"}
        jcenter()
}

buildscript {

   repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
    
    dependencies {
        classpath "org.asciidoctor:asciidoctor-gradle-plugin:1.5.2" 
   }
}


sourceCompatibility = "1.8"

compileJava {
   targetCompatibility = 1.8
}

dependencies {    
   compile "org.slf4j:slf4j-api:1.7.12"                      // logging api
   compile "ch.qos.logback:logback-classic:1.1.3"            // logging library
   
   testCompile "junit:junit:4.12"
 }


jdepend {
   toolVersion = "2.9.1"
}

jdependMain {
   reports {
      xml.enabled  = false
      text.enabled = true
   }
}

jdependTest {
   reports {
      xml.enabled  = false  
      text.enabled = true
   }
}

findbugs {
   toolVersion    = "3.0.1"
   ignoreFailures = true
   effort         = "max"
   reportLevel    = "high"
}

checkstyle {
   ignoreFailures = true      
   toolVersion    = "6.7"    // with java8 support
}

pmd {
   ignoreFailures = true
}


tasks.withType(FindBugs) {
    reports {
        xml.enabled  = false
        html.enabled = true
    }
}

jacocoTestReport {
    reports {
        xml.enabled      = true    // required for sonar&coveralls
        html.enabled     = true
        csv.enabled      = false
//        html.destination = "${buildDir}/reports/jacoco"
    }
}

// configured for local sonarQube 4.5 installation (defaults)
sonarRunner {
//    toolVersion = '2.4'
    sonarProperties {
        property "sonar.projectName",          "totask2 project time logging"
        property "sonar.projectKey",           "totask2"
        property "sonar.jacoco.reportPath",    "${buildDir}/jacoco/test.exec"
        property "sonar.junit.reportsPath",    "${buildDir}/test-results/"
        property "sonar.surefire.reportsPath", "${buildDir}/test-results/"
    }
}


asciidoctor {
    sourceDir    = new File("${projectDir}/src/docs")
    outputDir    = new File("${buildDir}/docs/asciidoc")
    logDocuments = true
} 


javadoc {
    failOnError         = false
    options.windowTitle = "totask2 api documentation ${version}"	
    options.links( "http://download.oracle.com/javase/8/docs/api/" )
    options.addBooleanOption("Xdoclint:none").setValue(true)
}


// CUSTOM TASKS
// ============

task allDocs {
    description "generation of full documentation (asciidoc and javadoc)"
    dependsOn javadoc, asciidoctor, projectReport 
}

task coverage {
    description "code coverage"
    dependsOn test, jacocoTestReport
}

task wrapper(type: Wrapper) {
    description "gradle wrapper generation (call if gradle version changes)"
    gradleVersion = '2.5'
}
